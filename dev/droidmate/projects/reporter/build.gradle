// DroidMate, an automated execution generator for Android apps.
// Copyright (C) 2012-2016 Konrad Jamrozik
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
//
// email: jamrozik@st.cs.uni-saarland.de
// web: www.droidmate.org
/*
  This project contains classes reused by multiple projects, none of which are run on an Android device.
  Because none of them are run on an Android Device, the classes in this project can and are compiled with Groovy and Java 8.
*/

buildscript {

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: "kotlin"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// Why these lines? See explanation for equivalent lines in ":projects:command:build.gradle"
configurations {forceJunitPriority}
sourceSets.test.runtimeClasspath = configurations.forceJunitPriority + sourceSets.test.runtimeClasspath
sourceSets.test.compileClasspath = configurations.forceJunitPriority + sourceSets.test.compileClasspath

/*
 This project uses test artifacts of core project.
 WISH this is a hack. Correct solution is described here: http://stackoverflow.com/a/15361666/986533
 */
evaluationDependsOn(":projects:core")

dependencies {

  compile project(":projects:lib-kotlin")
  
  forceJunitPriority 'junit:junit:4.12'
  testCompile 'junit:junit:4.12'
  testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
  testCompile 'org.hamcrest:hamcrest-library:1.3'
  testCompile project(":projects:core").sourceSets.test.output
  

  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  compile project(":projects:core")
  
  compile 'com.google.jimfs:jimfs:1.0'
  compile 'org.zeroturnaround:zt-exec:1.9'



}

test.configure {

  /* The tests have to be run from root project dir (droidmate) for the classpaths to be properly resolved.

   API reference:
   workingDir  http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html
   rootDir     http://www.gradle.org/docs/current/dsl/org.gradle.api.Project.html
   */
  workingDir rootDir

  /* Lurking bug: GRADLE-1682. */
  includes = ['**/ReporterTestSuite.class']

}

apply from: project(":projects").file("maven.gradle")

